plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.0.6'
    id 'org.openapi.generator' version '6.5.0'
}

apply plugin: 'io.spring.dependency-management'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = 'org.api_first.approach'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

dependencyLocking {
    lockAllConfigurations()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/openapi.yaml"
    outputDir = "$rootDir"
    ignoreFileOverride = "$rootDir/.openapi-generator-ignore"
    configOptions.putAll([
            invokerPackage : "${group}",
            basePackage    : "${group}",
            apiPackage     : "${group}.api",
            modelPackage   : "${group}.model",
            configPackage  : "${group}.config",
            delegatePattern: 'true',
            library        : 'spring-boot',
            useSpringBoot3 : 'true'
    ])
}

test {
    useJUnitPlatform()
}